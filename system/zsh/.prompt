## Prompt

# zsh Prompts https://wiki.archlinux.org/index.php/zsh#Prompts
# zsh Prompt Expansion http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html


# From http://dotfiles.org/~_why/.zshrc
# Sets the window title nicely no matter where you are
function title() {
  # escape '%' chars in $1, make nonprintables visible
  a=${(V)1//\%/\%\%}

  # Truncate command, and join lines.
  a=$(print -Pn "%40>...>$a" | tr -d "\n")

  case $TERM in
  screen)
    print -Pn "\ek$a:$3\e\\" # screen title (in ^A")
    ;;
  xterm*|rxvt)
    print -Pn "\e]2;$3\a" # plain xterm title ($3 for pwd)
    ;;
  esac
}

git_dirty() {
  if $(! git status -s &> /dev/null)
  then
    echo ""
  else
    if [[ $(git status --porcelain) == "" ]]
    then
      echo " %{$fg[green]%}$(git_prompt_info)%{$reset_color%}"
    else
      echo " %{$fg[red]%}$(git_prompt_info)%{$reset_color%}"
    fi
  fi
}

git_prompt_info () {
  ref=$(git symbolic-ref HEAD 2>/dev/null)
  echo "(${ref#refs/heads/})"
}

unpushed () {
  git cherry -v @{upstream} 2>/dev/null
}

need_push () {
  if [[ $(unpushed) == "" ]]
  then
    echo ""
  else
    echo " %{$fg_bold[magenta]%}{unpushed}%{$reset_color%}"
  fi
}

user_machine_name() {
  echo "%{$fg[cyan]%}%n%{$reset_color%}@%{$fg[blue]%}%m%{$reset_color%}"
}

directory_name() {
  echo "%{$fg[yellow]%}%~%{$reset_color%}"
}

export PROMPT='$(user_machine_name) $(directory_name)$(git_dirty)$(need_push) %{$reset_color%}> '

set_prompt () {
  export RPROMPT="%{$fg[blue]%}%*%{$reset_color%}" # 24 hour time with seconds
}

precmd() {
  title "zsh" "%m" "%~"
  set_prompt
}
